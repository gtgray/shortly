
plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'io.gitlab.arturbosch.detekt'
    id 'com.google.firebase.appdistribution'
}

apply from: '../config/quality/ktlint.gradle'
apply from: '../config/quality/checkstyle.gradle'
apply from: '../config/quality/jacoco.gradle'

android {
    compileSdkVersion 33

    defaultConfig {
        applicationId "tk.atna.shortlyapp"
        minSdkVersion 24
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"

        testApplicationId "tk.atna.shortlyapp.test"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        config {
            storeFile file(System.getenv('SIGN_STORE_FILE') ?: './shortly.jks')
            storePassword System.getenv('SIGN_STORE_PASSWORD') ?: 'shortly'
            keyAlias System.getenv('SIGN_KEY_ALIAS') ?: 'shortly'
            keyPassword System.getenv('SIGN_KEY_PASSWORD') ?: 'shortly'
        }
    }

    buildTypes {
        releaseQa {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
            generateReleaseNotes("$projectDir/build")
            firebaseAppDistribution {
                appId = "application_id_in_firebase_project"
                serviceCredentialsFile = file('service_account.json')
                releaseNotesFile = file("$projectDir/build/release_notes.txt")
            }
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        viewBinding true
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
            all { jvmArgs '-noverify' }
        }
        animationsDisabled = true
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }
}

dependencies {
    testImplementation 'junit:junit:4.13.2'

    androidTestUtil 'androidx.test:orchestrator:1.4.2'
    androidTestImplementation 'androidx.test:core:1.5.0'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.5.1'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4"
    androidTestImplementation 'com.kaspersky.android-components:kaspresso:1.4.3'

    testImplementation "junit:junit:4.13.2"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4"
    testImplementation "androidx.arch.core:core-testing:2.2.0"
    testImplementation 'io.mockk:mockk:1.13.3'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4"

    implementation 'androidx.fragment:fragment-ktx:1.5.5'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation "androidx.recyclerview:recyclerview:1.2.1"
    implementation 'com.google.android.material:material:1.8.0'

    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.5.1"

    implementation 'io.insert-koin:koin-android:3.3.3'

    implementation 'androidx.room:room-runtime:2.5.0'
    kapt 'androidx.room:room-compiler:2.5.0'
    implementation 'androidx.room:room-ktx:2.5.0'

    implementation "com.squareup.okhttp3:okhttp:4.10.0"
    implementation "com.squareup.okhttp3:logging-interceptor:4.10.0"
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"
}

detekt {
    toolVersion = "1.19.0"
    source = files("src/main/java")
    config = files("$projectDir/../config/quality/detekt.yml")
}

tasks.named("detekt").configure {
    reports {
        txt.required = false
        sarif.outputLocation = file("$projectDir/build/reports/detekt/detekt-report.sarif")
        xml.outputLocation = file("$projectDir/build/reports/detekt/detekt-report.xml")
        html.outputLocation = file("$projectDir/build/reports/detekt/detekt-report.html")
    }
}

task checkBeforePushFast(group: 'verification', description: 'Inspect your code before push') {
    dependsOn 'checkstyle', 'detekt', 'ktlint'
}