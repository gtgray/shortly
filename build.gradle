
buildscript {
    ext {
        kotlin_version = '1.7.21'
    }
}

plugins {
    id 'com.android.application' version '7.4.1' apply false
    id 'org.jetbrains.kotlin.android' version "$kotlin_version" apply false
    id 'io.gitlab.arturbosch.detekt' version '1.19.0' apply false
    id 'com.google.firebase.appdistribution' version '3.0.1' apply false
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

static void generateReleaseNotes(String dir) {
    // discover all tags by date
    def tags = "git tag --list --sort=-committerdate".execute().text.trim().tokenize('\n')
    // collect commit messages between last two tags
    def messages = ""
    if (tags.size() > 1) {
        messages = ("git log --no-merges --pretty=%s " + tags.get(0) + "..." + tags.get(1))
                .execute().text.trim().tokenize('\n')
    }
    // format release notes and write to file
    def releaseNotesDir = new File(dir)
    releaseNotesDir.mkdirs()
    def releaseNotesFile = new File(releaseNotesDir, 'release_notes.txt')
    releaseNotesFile.newWriter().withWriter { writer ->
        int i = 0
        messages.collect {
            if(it != null) {
                def line = ++i + "\\ " + it + "\n"
                writer << line
            }
        }
    }
    // print release notes file content
    new Scanner(releaseNotesFile).with {
        while (hasNextLine()) {
            println(nextLine())
        }
    }
}